<!DOCTYPE html>
<title>Office License Isn't Genuine</title>
<h3>Proposed explanation and solutions for Office C2R &#39;<span style="color:#e67e22">Your license isn&#39;t genuine</span>&#39; banner:</h3>

<hr />
<p><img alt="" src="https://i.imgur.com/gLFxssD.png" style="height:87px; width:1118px" /></p>

<h2>Solution:</h2>

<p>If you are seeing this banner on Office apps, the below solution can resolve it.</p>

<ul>
	<li>Use the latest <a href="https://forums.mydigitallife.net/posts/838808/" target="_blank">KMS_VL_ALL</a>&nbsp;(<a href="https://pastebin.com/cpdmr6HZ" target="_blank">Mirror</a>) by&nbsp;@<a href="https://forums.mydigitallife.net/members/abbodi1406.204274/" target="_blank">abbodi1406</a>&nbsp;to activate Office.<br />
	&nbsp;</li>
	<li><a href="Remove_Office_Non-Genuine_Banner_Password_1234.zip">Download</a>&nbsp;Remove_Office_Non-Genuine_Banner_Password_1234.zip file,<br />
	- Extract this .zip file, with the password 1234<br />
	- Find a file named &quot;<span style="font-family:Courier New,Courier,monospace">Remove_Office_Non-Genuine_Banner.cmd</span>&quot;, right-click on it, and select Run as administrator.<br />
	- Done.<br />
	&nbsp;</li>
</ul>

<p>It will work on every update channel/product&nbsp;of Office C2R Volume 2016/2019/2021/O365 (Mondo 2016)<br />
Thanks to <a href="https://forum.ru-board.com/topic.cgi?forum=35&amp;topic=81283&amp;start=6080#19" target="_blank">dialmak</a> for the solution.&nbsp;ðŸ¤—</p>

<hr />
<h3>Content&nbsp;of the scripts inside&nbsp;Remove_Office_Non-Genuine_Banner.zip file</h3>
<details>
    <summary>Click to open/close</summary>

<p><u><strong>Remove_Office_Non-Genuine_Banner.cmd</strong></u></p>

<pre>
<code>@echo off

::  https://windowsaddict.ml/office-license-is-not-genuine

::  Thanks to @dialmak for the solution
::  http://forum.ru-board.com/topic.cgi?forum=35&topic=81283&start=6080#19

::  Script concept:-

::  On Windows 7, since the support is ended for it, Office C2R only receives security updates, and updates are locked at version 2002 (build 12527.#####). (Tricks to get latest version on 7 are not working properly.)
::  And banner started appearing at Version 2101, Build 13628.20380, with the current update channel in February 2021
::  Hence Offices on Windows 7 are not affected by the banner and this script doesn't apply the solution

::  Solution requires to set KMS server (any name/IP except blacklisted public KMS server name (not IP)) for global (and also for it's 32bit registry key on 64bit Windows) and for specific Office SKUID
::  Banner affected Office versions are Office Volume C2R 2016/2019/2021, script applies the solution only if they exist


reg query HKU\S-1-5-19 1>nul 2>nul || (
echo Right click on this file and select 'Run as administrator'.
goto :end
)

set app=
setlocal EnableDelayedExpansion
for /f "tokens=2 delims==" %%G in ('"wmic path SoftwareLicensingProduct where (ApplicationID='0ff1ce15-a989-479d-af46-f275c6370663' and Description like '%%KMSCLIENT%%' and not Description like '%%Office 15%%' and PartialProductKey is not NULL) get ID /VALUE" 2^>nul') do (if defined app (call set "app=!app! %%G") else (call set "app=%%G"))

if not defined app (
echo Could not detect Volume version of Office C2R 2016/2019/2021 on Windows 8 and later.
goto :end
)

sc query ClickToRunSvc 1>nul 2>nul
if %errorlevel%==1060 (
echo Could not detect Office C2R version.
goto :end
)

for %%# in (%app%) do (
reg add "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform\0ff1ce15-a989-479d-af46-f275c6370663\%%#" /f /v KeyManagementServiceName /t REG_SZ /d "0.0.0.0"
)

reg add "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /f /v KeyManagementServiceName /t REG_SZ /d "0.0.0.0"
reg add "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /f /v KeyManagementServiceName /t REG_SZ /d "0.0.0.0" /reg:32

:end
echo:
echo Press any key to exit...
pause >nul</code></pre>

<p><u><strong>Undo.cmd</strong></u></p>

<pre>
<code>@echo off

::  https://windowsaddict.ml/office-license-is-not-genuine

reg query HKU\S-1-5-19 1>nul 2>nul || (
echo Right click on this file and select 'Run as administrator'.
goto :end
)

reg delete "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform\0ff1ce15-a989-479d-af46-f275c6370663" /f
reg delete "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /f /v KeyManagementServiceName
reg delete "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\SoftwareProtectionPlatform" /f /v KeyManagementServiceName /reg:32

:end
echo:
echo Press any key to exit...
pause >nul</code></pre>

</details>
<br />
<hr />
<p>
<span style="font-size:18px"><strong>P.S.</strong></span></p>

<ul>
	<li>If you have any conflicting info or any other useful info then please email me at <strong>windowsaddict@protonmail.com</strong></li>
</ul>

<p>Updated on 12-Jul-2021</p>
